10 REM ††††††††††††††††††
20 REM †Ã…”‘…Œ«†–“œ«“¡Õ’†
30 REM †††††††ÚÔ††††††††
40 REM ††“œ¬œ‘“œŒ†À∂≥∞¥††
50 REM †®„©†–“œ††∞∏Ø±π∏∏†
60 REM †’–“† Æ”Æ†∞≤Ø±ππ¥†
70 REM ††††††††††††††††††
80 GRAPHICS 0:CLR :CLOSE #1:CLOSE #2:CLOSE #3:POKE 195,0:SETCOLOR 3,6,4:SETCOLOR 2,6,4
85 POKE 752,1:POKE 82,2:POKE 16,64:POKE 53744,64
90 DL=PEEK(560)+256*PEEK(561)
100 POKE DL+10,55:POSITION 4,5:? "listing programu"
110 POKE DL+12,54:POSITION 29,6:? "  VER. 3.4*"
120 POSITION 20,10:? "ATARI KLUB  BRNO"
130 POSITION 17,14:? "  pro ROBOTRON K6304  "
140 POSITION 20,20:? "(C) PRO  08/1988"
150 POSITION 20,21:? "UPR J.S. 02/1994"
155 GOSUB 3000
160 FOR PAU=1 TO 1500:NEXT PAU
165 OPEN #3,4,0,"K:"
170 DIM A$(160),HL$(200),NS$(15),NP$(13),V$(15),Y$(135),Z$(2),ZNAK$(10),POM$(20)
180 GRAPHICS 0:SETCOLOR 2,1,4:POKE 752,0:POKE 82,0:POKE 16,64:POKE 53744,64
190 POSITION 12,1:? "†⁄¡ÀÃ¡ƒŒ…†’ƒ¡ ≈†"
200 D=0:POSITION 4,3:? "Zdrojove zarizeni: D8"
210 POSITION 4,4:? "(C, T, D1-D4, D8)":POSITION 21,3:? " ";:INPUT Z$:IF Z$<>"C" AND Z$<>"T" THEN D=1
220 IF Z$<>"D1" AND Z$<>"D2" AND Z$<>"D3" AND Z$<>"D4" AND Z$<>"D8" AND D<>0 THEN POSITION 23,3:? "           ˝":GOTO 200
230 POSITION 4,6:? "Nazev programu:    ";:INPUT NP$
240 IF Z$<>"C" AND Z$<>"T" AND (LEN(NP$)<1 OR LEN(NP$)>12) THEN POSITION 23,6:? "                     ˝":GOTO 230
250 TRAP 260:POSITION 4,8:? "Cislovat od listu: 1":POSITION 21,8:? " ";:INPUT Z:Z=Z-1
260 IF PEEK(195)>0 THEN POKE 195,0:POSITION 23,8:? "           ˝":GOTO 250
270 TRAP 280:POSITION 4,10:? "Levy okraj:        1":POSITION 21,10:? " ";:INPUT L
280 IF L<1 OR L>60 OR PEEK(195)>0 THEN POKE 195,0:POSITION 23,10:? "           ˝":GOTO 270
290 TRAP 300:POSITION 4,12:? "Pravy okraj:       59":POSITION 21,12:? " ";:INPUT P
300 IF P<L+20 OR P>132 OR PEEK(195)>0 THEN POKE 195,0:POSITION 23,12:? "           ˝":GOTO 290
310 TRAP 320:POSITION 4,14:? "Pocet radku:       62":POSITION 21,14:? " ";:INPUT S
320 IF S<10 OR S>66 OR PEEK(195)>0 THEN POKE 195,0:POSITION 23,14:? "           ˝":GOTO 310
330 DL=P-L-LEN(STR$(Z))-10:IF DL>115 THEN DL=115
340 POSITION 4,16:? "Vystupni zarizeni: P":POSITION 21,16:? " ";:INPUT V$
350 IF V$(1,1)<>"E" AND V$(1,1)<>"P" THEN POKE 752,1:POSITION 23,16:? "⁄¡“…⁄≈Œ…†≈†ÓÂ‚Ô†–˝":GOSUB 2000
360 IF V$(1,1)<>"E" AND V$(1,1)<>"P" THEN POSITION 23,16:? "                 ":POKE 752,0:GOTO 340
370 POSITION 4,18:? "Hlavicka (max. ";DL;" znaku):"
380 POSITION 3,20:? " ";:INPUT HL$
390 IF LEN(HL$)>DL THEN POSITION 4,19:? "˝":FOR F=1 TO LEN(HL$)+20:? " ";:NEXT F:GOTO 380
400 POKE 752,1:? " ":FOR PAU=1 TO 250:NEXT PAU:? "}"
410 IF V$(1,1)="E" THEN 470
420 POSITION 11,10:? "†††ƒ“’»††–¡–…“’†††"
430 POSITION 11,12:? "± JEDNOTLIVE LISTY"
440 POSITION 11,14:? "≤ SKLADANY PAPIR"
450 GET #3,Q:IF Q<49 OR Q>50 THEN 450
460 GOSUB 1190
470 GOSUB 2100:? "}":TRAP 480:OPEN #2,8,0,V$:POKE 16,64:POKE 53744,64:POKE 752,1:SETCOLOR 2,12,4
475 IF PEEK(195)=0 THEN 510
480 POSITION 8,11:? "††††⁄¡–Œ≈‘≈†‘…”À¡“Œ’††††":POSITION 8,13:? "TISKARNA ZAPNUTA  “≈‘’“Œ˝":GET #3,Q1
490 POKE 195,0:CLOSE #2:IF Q1<>155 THEN ? "˝":GOTO 480
500 GOTO 470
510 IF V$(1,1)="P" AND LEN(POM$)>0 THEN FOR I=1 TO LEN(POM$):PUT #2,ASC(POM$(I,I)):NEXT I
520 NS$=Z$:IF Z$="C" THEN 530
525 NS$(LEN(NS$)+1)=":":NS$(LEN(NS$)+1)=NP$
530 TRAP 540:OPEN #1,4,0,NS$:IF PEEK(195)=0 THEN 590
540 POSITION 3,11:? "÷≈†⁄ƒ“œ œ÷≈Õ†⁄¡“…⁄≈Œ…†Œ≈Œ…†–“œ«“¡Õ"
550 POSITION 3,13:? "PROGRAM VLOZEN “≈‘’“Œ,  NAVRAT ≈”√˝":GET #3,Q1:IF Q1<>155 AND Q1<>27 THEN 550
560 POKE 195,0:CLOSE #1
570 IF Q1=27 THEN 180
580 ? "}":GOTO 530
590 POKE 766,1:D=0:IF V$(1,1)="P" THEN POKE 559,34
600 FOR F=1 TO 10000
610 IF C=0 THEN GOSUB 860
620 TRAP 630:A$="":INPUT #1;A$:GOSUB 1060
630 IF PEEK(195)>0 THEN POKE 195,0:POP :GOTO 790
640 IF B<=P-L-2 THEN Y$(1)=" ":Y$(L+3)=" ":Y$(2)=Y$:Y$(L)=CHR$(K1+128):Y$(L+1)=CHR$(K2+128):Y$(L+3)=A$
650 IF B>P-L-2 THEN 660
652 C=C+1:IF V$(1,1)="P" THEN GOSUB 980:GOTO 660
655 ? #2;Y$
660 IF B<=P-L-2 THEN 770
670 Y$="":Y$(1)=" ":Y$(L+3)=" ":Y$(2)=Y$:Y$(L)=CHR$(K1+128):Y$(L+1)=CHR$(K2+128)
680 Y$(L+3)=A$(1,P-L-2):C=C+1:Y=0:IF V$(1,1)="P" THEN GOSUB 980:GOTO 690
685 ? #2;Y$
690 D=INT((B-(P-L-2))/(P-L-I-2))+1
700 REM REPEAT
710 Y=Y+1:Y$="":Y$(1)=" ":Y$(I+L+3)=" ":Y$(2)=Y$
720 G=P-L-2+Y*(P-L-2-I)-(P-L-I-3):H=P-L-2+Y*(P-L-2-I):IF H>B THEN Y$(I+L+3)=A$(G,B):GOTO 740
730 Y$(I+L+3)=A$(G,H)
740 C=C+1:IF V$(1,1)="P" THEN GOSUB 980:GOTO 750
745 ? #2;Y$
750 IF H=B THEN D=D-1
760 IF Y<D THEN 710
770 IF C<=S THEN 780
774 IF V$(1,1)="E" THEN 777
775 FOR X=1 TO 72-C:PUT #2,13:PUT #2,10:NEXT X:C=0:IF Q=49 THEN GOSUB 920:GOTO 780
777 FOR X=1 TO 72-C:? #2:NEXT X:C=0
780 NEXT F
790 CLOSE #1
800 IF C<=S AND V$(1,1)="P" THEN FOR X=1 TO 72-C:PUT #2,10:NEXT X:C=0:GOTO 810
805 IF C<=S AND V$(1,1)="E" THEN FOR X=1 TO 72-C:? #2:NEXT X:C=0
810 CLOSE #2
820 ? "}":POKE 766,0:POKE 559,34:POSITION 12,12:? "DALSI VYPIS? ¡/Œ˝":GET #3,Q1:? "}"
830 IF Q1=65 THEN 180
840 GRAPHICS 0:CLOSE #1:CLOSE #2:CLOSE #3
850 END 
860 REM PROC HLAVICKA
870 Z=Z+1:POKE 752,1
880 Y$="":Y$(1)=" ":Y$(P)=" ":Y$(2)=Y$
890 Y$(L)=HL$:Y$(P+1-LEN(STR$(Z))-8)="Strana:":Y$(P+1-LEN(STR$(Z)),P)=STR$(Z)
900 C=2:IF V$(1,1)="P" THEN GOSUB 980:PUT #2,13:PUT #2,10:GOTO 910
905 ? #2;Y$:? #2
910 RETURN 
920 REM PROC VYMENA
930 ? "}":POKE 766,0:POKE 559,34
940 POSITION 5,11:? "÷Ãœ⁄‘≈†Œœ÷Ÿ†–¡–…“†ƒœ†‘…”À¡“ŒŸ":POSITION 5,13:? "TISKARNA PRIPRAVENA    “≈‘’“Œ˝"
950 GET #3,Q1:IF Q1<>155 THEN 950
960 ? "}":POKE 559,0:POKE 766,1
970 RETURN 
980 REM PROC ZNAK
982 AGEN=8*LEN(Y$):AGEH=INT(AGEN/256):AGEL=AGEN-256*AGEH
984 PUT #2,27:PUT #2,75:PUT #2,AGEL:PUT #2,AGEH
990 FOR U=1 TO LEN(Y$)
1000 A=ASC(Y$(U,U)):KUK=USR(1584,A)
1020 IF A=193 THEN GOSUB 4000:GOTO 1040
1030 FOR BLB=1536 TO 1543:PUT #2,PEEK(BLB):NEXT BLB
1040 NEXT U:PUT #2,13:PUT #2,10
1050 RETURN 
1060 REM PROC KOD
1070 E=0:J=0:K=0:K1=0:K2=0:M=0
1080 FOR I=1 TO 30
1090 Y$=A$(I,I):IF (ASC(Y$)>32 AND ASC(Y$)<48) OR ASC(Y$)>57 THEN POP :I=I-1:GOTO 1120
1100 IF ASC(Y$)<>32 THEN E=E+1
1110 NEXT I
1120 B=LEN(A$):Y$="":Y$(1)=" ":Y$(3+I)=" ":Y$(2)=Y$
1130 Y$(1,E)=A$(1,E):Y$(E+1)=A$(I,B):N=LEN(Y$):IF Y$(N)=" " THEN N=N-1
1140 REM REPEAT
1150 M=M+1:K=K+(M*ASC(Y$(M,M)))
1160 IF M<N THEN 1150
1170 J=K-INT(K/676)*676:K1=INT(J/26):K2=J-K1*26+65:K1=K1+65
1180 RETURN 
1190 REM PROC DOPLNEK
1200 ? "}":POSITION 10,10:? " ... mezera ... ¡/Œ"
1210 GET #3,ESC:IF ESC<>ASC("A") AND ESC<>ASC("N") THEN 1210
1220 POSITION 12,16:? "Ridici retezec :":POSITION 12,18:INPUT POM$
1230 RETURN 
2000 REM PAUSE 70
2010 FOR PAU=1 TO 350:NEXT PAU
2020 RETURN 
2100 REM PAUSE 40
2110 FOR PAU=1 TO 200:NEXT PAU
2120 RETURN 
2300 REM INPUT #1,A$
2310 GET #1,CAR:A$(LEN(A$)+1)=CHR$(CAR)
2320 IF CAR<>155 THEN 2310
2330 RETURN 
3000 REM ISTALACE PODPROGRAMU OTACENI
3010 RESTORE 3100:FOR I=1584 TO 1693:READ XXX:POKE I,XXX:NEXT I:RETURN 
3100 DATA 104,104,104,141,16,6,41,128,141,17,6,169,0,133,212,133,213,173,16,6,41,127,201,96,176,11,201,32,176
3110 DATA 4,9,64,208,3,56,233,32,10,10,38,213,10,38,213,133,212,24,165,213,109,244,2,133,213,160,0,177,212,153
3120 DATA 8,6,200,192,8,208,246,173,17,6,240,16,162,0,169,255,56,253,8,6,157,8,6,232,224,8,208,242,160,0,162,0
3130 DATA 138,30,8,6,42,232,224,8,208,247,153,0,6,200,192,8,208,236,96
4000 REM INVERZNI A
4010 PUT #2,255:PUT #2,225:PUT #2,193:PUT #2,151:PUT #2,151:PUT #2,193:PUT #2,225:PUT #2,255
4020 RETURN 
