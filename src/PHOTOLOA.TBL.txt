1 REM *******************************
3 REM *†–»œ‘œ”†Ãœ¡ƒ≈“†∆œ“†Œ≈◊”“œœÕ††*
4 REM *≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠≠*
5 REM *†††ÁÂÔÚÁÂÚ¥≤∞¨††≤∞Æ∂Æ±ππ≥††††*
6 REM *******************************
7 REM 
10 DIM FIL$(20),NAM$(20)
20 ? CHR$(125);"INPUT FILE:"
30 INPUT "DEV, FILENAME: ";FIL$
40 ? :? "OUTPUT FILE:"
50 INPUT "DEV, FILENAME: ";NAM$
55 ? :? "PRESS START FOR SAVING PICTURE";CHR$(155);"THEN PRESS START FOR NEW PICTURE":PAUSE 100
70 CLOSE #1:OPEN #1,4,0,FIL$
80 RESTORE 1000
90 FOR I=1 TO 4
100   GET #1,A:READ B
110   IF A<>B THEN POP :CLOSE #1:? :? "NOT NEWSROOM PHOTO!":STOP 
120 NEXT I
130 GET #1,A:GET #1,A
140 GET #1,A:GET #1,B:LONG=A+256*B
150 GET #1,A:GET #1,B:POCRAD=B-A
160 GET #1,A:GET #1,B:POM=B-A:BAJT=INT(POM/8)+1
170 IF LONG<>(POCRAD+1)*BAJT THEN CLOSE #1:? "ERROR IN PICTURE DATA!":STOP 
180 WHILE A<>255
190   GET #1,A
200 WEND 
210 GRAPHICS 24:VID=DPEEK(88)
220 FOR I=0 TO POCRAD
230   KAM=VID+40*I
240   BGET #1,KAM,BAJT
250 NEXT I
260 CLOSE #1:GOSUB 3000:GOSUB 2000
270 IF PEEK(53279)=7 THEN 270
280 OPEN #1,8,0,NAM$
290 BPUT #1,VID,7680
300 CLOSE #1:GOSUB 2000
810 IF PEEK(53279)=7 THEN 810
820 CLOSE :CLR :RUN 
999 STOP 
1000 DATA 255,255,0,160
2000 SOUND 0,90,10,15:PAUSE (25):SOUND :RETURN 
3000 REM INVERZE OBRAZU
3010 FOR I=0 TO POCRAD
3030   FOR J=0 TO BAJT-1
3040     KAM=VID+40*I+J
3050     POKE KAM,255-PEEK(KAM)
3060   NEXT J
3070 NEXT I
3080 B=BAJT*8:A=POM+1:COLOR 2
3090 FOR I=A TO B
3100   PLOT I,0:DRAWTO I,POCRAD
3110 NEXT I
3120 RETURN 
