1 REM ************************
2 REM *                      *
3 REM *    SLOVNIK   V1.0    *
4 REM *                      *
5 REM *      georger420      *
6 REM *                      *
7 REM *      (C)   1987      *
8 REM *                      *
9 REM ************************
20 CLR :POCET=1000:DELKA=4+13
30 DIM IN$(127),POM$(127),U$(2*POCET),T$(DELKA*POCET)
40 OPEN #1,4,0,"K:"
100 GRAPHICS 2+16
110 FOR I=0 TO 19:POSITION I,0:? #6;"*":NEXT I
120 FOR I=1 TO 9:POSITION 0,I:? #6;"*":POSITION 19,I:? #6;"*":NEXT I
130 POSITION 0,10:? #6;"** (C) GEORGER420 **"
140 POSITION 6,6:? #6;"SLOVNIK":GOSUB 3100
150 FOR DEL=1 TO 1000:NEXT DEL
300 REM MENU
310 GRAPHICS 0:SETCOLOR 2,0,10:SETCOLOR 4,0,10:SETCOLOR 1,0,4:POKE 752,1:POKE 82,0
315 ? 
320 POSITION 0,0:? ""
330 POSITION 0,1:? "|                                      |"
340 POSITION 0,2:? "|           M    E    N    U           |"
350 POSITION 0,3:? "|                                      |"
360 POSITION 0,4:? ""
370 POSITION 9,7:? "0...KONEC":POSITION 9,9:? "1...VKLADANI":POSITION 9,11:? "2...LISTOVANI"
380 POSITION 9,13:? "3...TRIDENI":POSITION 9,15:? "4...VYHLEDAVANI"
390 POSITION 9,17:? "5...UKLADANI NA KAZETU":POSITION 9,19:? "6...NAHRAVANI Z KAZETY"
400 POSITION 9,21:? "7...SPRAVA SLOVNIKU"
410 GET #1,SEL
420 IF (SEL>=50 AND SEL<=53) AND (T$="" OR U$="") THEN GOSUB 3000:GOSUB 2600:GOTO 300
430 ON (SEL-47) GOTO 500,800,1000,1200,1500,2000,2300,4000
440 GOTO 410
500 REM KONEC
510 ? "}"
515 POSITION 5,6:? ""
520 POSITION 5,7:? "|Pokud byl obsah slovniku zme-|"
530 POSITION 5,8:? "|nen,  je treba jej pred ukon-|"
540 POSITION 5,9:? "|cenim prace ulozit na kazetu,|"
550 POSITION 5,10:? "|pripadne,  jeste pred uklada-|"
560 POSITION 5,11:? "|nim setridit!                |"
570 POSITION 5,12:? ""
580 POSITION 7,19:? "═конец╝╝╝╟╛═мену╝╝╝ЦОКОЛИЖ═"
590 GET #1,SEL:IF SEL<>48 THEN 300
600 GRAPHICS 0:CLOSE #1:POKE 82,2:POKE 752,0:CLR :END 
800 REM VKLADANI
810 ? "}":POKE 752,0
820 DU=LEN(U$):DT=LEN(T$)
830 IF DU/2>=POCET OR (DT+2)>=(DELKA*POCET) THEN 920
840 U=LEN(T$)+1:UH=INT(U/256):UL=U-UH*256
850 U$(LEN(U$)+1)=CHR$(UL):U$(LEN(U$)+1)=CHR$(UH)
860 ? :? "сложо╨═══";:INPUT IN$:IF IN$="" THEN 910
870 GOSUB 900
880 ? "жызнам╨══";:INPUT IN$:IF IN$="" THEN 910
890 GOSUB 900:GOTO 820
900 T$(LEN(T$)+1)=CHR$(LEN(IN$)+2):T$(LEN(T$)+1)=IN$:IN$="":RETURN 
910 U$(DU+1)="":T$(DT+1)="":GOTO 300
920 ? :? "═сложник═йе═йиз═══п═л═н═ы═║═":GOSUB 3000:FOR DEL=1 TO 500:NEXT DEL:GOTO 300
1000 REM LISTOVANI
1010 ? "}":I=0:? "MENU...ретурн, LISTOVANI...COKOLIV!"
1020 GET #1,SEL:IF SEL=155 THEN 300
1030 I=I+1:GOSUB 2800:? T$(U+1,U+U1-2)
1040 IF I=LEN(U$)/2 THEN I=0
1050 GOTO 1020
1200 REM TRIDENI
1210 ? "}":POSITION 13,6:? "═ц═е═к═е═й═║═":F=1:POC=LEN(U$)/2
1230 POSITION 16,11:? "FAZE: ";F;"   ":SETR=0:GOSUB 2900
1240 FOR I=1 TO POC:GOSUB 2800:IF I=POC THEN I1=1:GOTO 1260
1250 I1=I+1
1260 UU=ASC(U$(2*I1-1))+ASC(U$(2*I1))*256:UU1=ASC(T$(UU))
1270 IF T$(UU+1,UU+UU1-2)<T$(U+1,U+U1-2) AND I1<>1 THEN GOSUB 1310
1280 NEXT I:IF SETR=0 THEN 1300
1290 POC=POC-1:F=F+1:GOTO 1230
1300 POSITION 9,16:? "═с═е═т═р═и═д═е═н═о═║═":GOSUB 3100:FOR DEL=1 TO 350:NEXT DEL:GOTO 300
1310 IN$=U$(2*I1-1,2*I1):U$(2*I1-1,2*I1)=U$(2*I-1,2*I):U$(2*I-1,2*I)=IN$:IN$="":SETR=SETR+1:RETURN 
1500 REM VYHLEDAVANI
1600 REM V SETRIDENEM SLOVNIKU
1605 ? "}":POKE 752,0
1610 ? :? "сложо╨═══";:INPUT IN$:IF IN$="" THEN 300
1620 IA=1:IB=LEN(U$)/2+1:IC=0
1630 POM$="":I=INT((IA+IB)/2):IF I=IC THEN 1680
1640 GOSUB 2800:POM$=T$(U+1,U+U1-2)
1650 IF IN$<POM$ THEN IB=I:IC=I:GOTO 1630
1660 IF IN$>POM$ THEN IA=I:IC=I:GOTO 1630
1670 U2=ASC(T$(U+U1-1)):? "жызнам╨══";T$(U+U1,U+U1+U2-3):IN$="":GOTO 1610
1680 GOSUB 3000:? :? "═сложо═неналезено║═":GOTO 1610
2000 REM UKLADANI NA KAZETU
2010 GOSUB 2390
2020 POCU=INT(LEN(U$)/100):ZBU=LEN(U$)-POCU*100
2030 POCT=INT(LEN(T$)/100):ZBT=LEN(T$)-POCT*100
2040 OPEN #2,8,0,"C:"
2050 ? #2;POCU:? #2;ZBU
2060 IF POCU=0 THEN 2080
2070 FOR I=1 TO POCU:K=(I-1)*100+1:POM$=U$(K,K+99):? #2;POM$:NEXT I
2080 K=100*POCU:POM$=U$(K+1,K+ZBU):? #2;POM$
2090 FOR DEL=1 TO 200:NEXT DEL
2100 ? #2;POCT:? #2;ZBT
2110 IF POCT=0 THEN 2130
2120 FOR I=1 TO POCT:K=(I-1)*100+1:POM$=T$(K,K+99):? #2;POM$:NEXT I
2130 K=100*POCT:POM$=T$(K+1,K+ZBT):? #2;POM$
2140 CLOSE #2:POSITION 11,18:? "═у═л═о═з═е═н═о═║═":GOSUB 3100
2150 FOR DEL=1 TO 350:NEXT DEL:GOTO 300
2300 REM NAHRAVANI Z KAZETY
2310 GOSUB 2390
2320 OPEN #2,4,0,"C:"
2330 INPUT #2,POCU:INPUT #2,ZBU
2340 FOR I=1 TO POCU+1:INPUT #2,POM$:U$(LEN(U$)+1)=POM$:NEXT I
2350 INPUT #2,POCT:INPUT #2,ZBT
2360 FOR I=1 TO POCT+1:INPUT #2,POM$:T$(LEN(T$)+1)=POM$:NEXT I
2370 CLOSE #2:POSITION 11,18:? "═н═а═х═р═а═н═о═║═":GOSUB 3100
2380 FOR DEL=1 TO 350:NEXT DEL:GOTO 300
2390 REM PRETACENI
2400 ? "}"
2405 POSITION 6,3:? "════════════════════════════"
2410 POSITION 6,4:? "═▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓┘═"
2420 POSITION 6,5:? "═Эпретоц═казету═на═зацатекЭ═"
2430 POSITION 6,6:? "═Эдатожехо═соубору═а═потомЭ═"
2440 POSITION 6,7:? "═Эстлац═ RETURN ║═════════Э═"
2450 POSITION 6,8:? "═ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓┐═"
2460 POSITION 6,9:? "════════════════════════════"
2470 GET #1,SEL:IF SEL=155 THEN RETURN 
2480 GOTO 2470
2600 REM CHYBNA VOLBA Z MENU
2610 ? "}"
2615 POSITION 8,4:? "════════════════════════"
2620 POSITION 8,5:? "═▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓┘═"
2630 POSITION 8,6:? "═Эсложник═йе═праздны║║Э═"
2640 POSITION 8,7:? "═Энутно═жолит═жкладаниЭ═"
2650 POSITION 8,8:? "═Энебо═нахражани═з═ка╜Э═"
2660 POSITION 8,9:? "═Эзеты║═══════════════Э═"
2670 POSITION 8,10:? "═ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓┐═"
2680 POSITION 8,11:? "════════════════════════"
2690 POSITION 7,18:? "═стлац═йакоуколиж═клажесу║═"
2700 GET #1,SEL:RETURN 
2800 REM UKAZATEL
2810 U=ASC(U$(2*I-1))+ASC(U$(2*I))*256:U1=ASC(T$(U))
2820 RETURN 
2900 REM ZVUK 1 - PIIPAANII
2910 SOUND 1,29,10,15:FOR DEL=1 TO 25:NEXT DEL:SOUND 1,0,0,0
2920 RETURN 
3000 REM ZVUK 2 - CHYBA
3010 SOUND 1,114,10,12:FOR DEL=1 TO 30:NEXT DEL:SOUND 1,0,0,0
3020 SOUND 1,57,10,15:FOR DEL=1 TO 110:NEXT DEL:SOUND 1,0,0,0
3030 RETURN 
3100 REM ZVUK 3 - HOTOVO
3110 SOUND 1,114,10,15:FOR DEL=1 TO 40:NEXT DEL:SOUND 1,0,0,0
3120 FOR DEL=1 TO 4:NEXT DEL
3130 SOUND 1,72,10,15:FOR DEL=1 TO 40:NEXT DEL:SOUND 1,0,0,0
3140 FOR DEL=1 TO 4:NEXT DEL
3150 SOUND 1,96,10,15:FOR DEL=1 TO 80:NEXT DEL:SOUND 1,0,0,0
3160 RETURN 
4000 REM SPRAVA SLOVNIKU
4010 ? "}"
4020 POSITION 10,8:? "════════════════════"
4030 POSITION 10,9:? "═▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓┘═"
4040 POSITION 10,10:? "═Эспража══сложникуЭ═"
4050 POSITION 10,11:? "═Эйесте═небыла═ре╜Э═"
4060 POSITION 10,12:? "═Эализожана║══════Э═"
4070 POSITION 10,13:? "═ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓┐═"
4080 POSITION 10,14:? "════════════════════"
4090 FOR DEL=1 TO 350:NEXT DEL:GOTO 300
