1 REM DEMO - CIRCLE AND ELLIPSE
10 GRAPHICS 8+16:COLOR 3:SETCOLOR 2,0,0:DEG 
20 XS=160:YS=96:BETA=5
30 R=90:GOSUB 30000
40 A=90:FOR B=A-10 TO 0 STEP -9.99:GOSUB 31000:NEXT B
50 B=90:FOR A=B-10 TO 0 STEP -9.99:GOSUB 31000:NEXT A
999 GOTO 999
30000 REM SUB: CIRCLE
30005 REM VSTUP: XS,YS,R,BETA
30010 X0=R:Y0=0:S=SIN(BETA):C=COS(BETA):PLOT XS+X0,YS+Y0
30020 FOR Q=BETA TO 360 STEP BETA:XX=X0*C-Y0*S:YY=X0*S+Y0*C:DRAWTO XS+XX,YS+YY:Y0=YY:X0=XX:NEXT Q:RETURN 
31000 REM SUB: ELLIPSE
31005 REM VSTUP: XS,YS,A,B,BETA
31010 X0=A:Y0=0:S2=(B/A)*SIN(BETA):S1=(A/B)*SIN(BETA):C=COS(BETA):PLOT XS+X0,YS+Y0
31020 FOR Q=BETA TO 360 STEP BETA:XX=X0*C-Y0*S1:YY=X0*S2+Y0*C:DRAWTO XS+XX,YS+YY:X0=XX:Y0=YY:NEXT Q:RETURN 
